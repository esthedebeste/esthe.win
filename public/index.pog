$$space from "../lib/Space.pog"
$$altlangs from "../lib/AltLangs.pog"
$$l10n(en nl) {
    if (language === "en") 
        `${en}`
    else 
        `${nl}`
}

!doctype(html)
html(lang=(language)) {
    head {
        meta(charset="UTF-8")
        meta(name="viewport" content="width=device-width, initial-scale=1.0")
        $altlangs
        link(rel="shortcut icon" href="/favicon.jpg" type="image/jpeg")
        title "esthe win!!"
        style {
			body {
				background-color: #000;
				color: #f0f;
				font-family: cursive;
                display: flex;
                flex-direction: column;
                align-items: center;
                justify-content: center;
				font-size: 1.5em;
                text-shadow: 0 0 2em currentColor, 0 0 2em currentColor;
			}

            canvas {
                position: fixed;
                left: 0;
                top: 0;
                width: 100vw;
                height: 100vh;
                z-index: -1;
            }

			a {
				color: #f0f !important;
			}

            main {
                width: fit-content;
            }

			small,
			footer {
				font-size: 0.66em;
				display: block;
			}

            footer {
                margin-top: 1em;
            }

            .icon {
                height: 1em;
                vertical-align: middle;
            }

            .nom {
                margin: 0;
                padding: 0;
            }

            .totp {
                margin: 0;
                padding: 0;
                position: relative;
                width: fit-content;
            }
            
            .totp progress {
                position: absolute;
                transform: translateY(100%);
                bottom: 0;
                left: 0;
                width: 100%;
                height: 5px;
                border: none;
                outline: solid 1px #ad00c7;
                background-color: #fff;
                -webkit-appearance: none;
            }

            .totp progress::-moz-progress-bar {
                background-color: #ad00c7;
            }

            .totp progress::-webkit-progress-value {
                background-color: #ad00c7;
            }

            .totp progress::-webkit-progress-bar {
                background-color: #fff;
            }

            .right {
                float: right;
            }

            ::selection {
                background-color: #b00b1355;
            }
        }
    }
    body {
        $space

        main {
            img.right(src="/WAVE.gif" alt="hyper wave gif" width="216" height="128")
            h1 "esthe win!!" with script {
                setInterval(() => {
                    h1.style.color = `hsl(${(Date.now() / 1000 * 25 + 180) % 360}, 100%, 50%)`
                }, 1000 / 30)
            }
            $$icon(id url) {
                if(url) {
                    a(href=`https://${url}` target="_blank") {
                        img.icon(src=`/icons/${id}.svg` title=(id) alt=(id))
                    }
                } else {
                    img.icon(src=`/icons/${id}.svg` title=(id) alt=(id))
                }
            }

            p {
                $l10n(en="welcome to esther winsser's little corner! " nl="welkom in esther winsser's hoekje op 't internet! ")
                $icon#bisexual $icon#transgender br
                $l10n(en="i'm a dutch programmer." nl="ik ben een nederlandse programmeur.")
                br
                $l10n(en="some languages i know: " nl="wat talen die ik ken: ")
                $icon#svelte(url="svelte.dev")
                $icon#css(url="developer.mozilla.org/docs/Web/CSS")
                $icon#javascript(url="developer.mozilla.org/docs/Web/JavaScript")
                $icon#typescript(url="typescriptlang.org")
                $icon#civet(url="civet.dev")
                $icon#go(url="go.dev")
                $icon#rust(url="rust-lang.org")
                $icon(id="C++" url="cppreference.com")
                $icon#fy(url="github.com/esthedebeste/fylang")
                $icon#java(url="java.com")
                $icon#kotlin(url="kotlinlang.org")
                $icon#c-sharp(url="docs.microsoft.com/dotnet/csharp")
                $icon#python(url="python.org")
                $icon#nederlands(url="nl.wikipedia.org/wiki/Nederlands")
                $icon#english(url="en.wikipedia.org/wiki/English_language")
            }

            h2.nom { a(href="https://npmjs.com/package/@esthe/totp" target="_blank") "totp" }
            p.totp "code: " { 
                span(data-secret=(TOTP_SECRET)) `${await generateTOTP(TOTP_SECRET)}` with script {
                    import("/totp.js").then(({ generateTOTP }) => {
                        async function generate() {
                            span.textContent = await generateTOTP(dataset.secret)
                            setTimeout(generate, 30_000 - (Date.now() % 30_000))
                        }
                        generate()
                    })
                } br
                label#totp-label(for="totp-progress") { $l10n(en="time: " nl="tijd: ") span "0" $l10n(en=" seconds" nl=" seconden") }
                progress#totp-progress(max="30000" value="0") with script {
                    const label = document.querySelector("#totp-label span")
                    setInterval(() => {
                        const totpProgress = Date.now() % 30_000
                        progress.value = totpProgress
                        label.textContent = 30 - Math.floor(totpProgress / 1000)
                    }, 1000 / 30)
                }
            }
        }
        footer "[source code @ " {
            a(href="https://github.com/esthedebeste/esthe.win") "github:esthedebeste/esthe.win"
        } " | " { $l10n(en="old site" nl="oude site") } " @ " { a(href="https://site2223.esthe.win") "site2223.esthe.win" } "]"
    }
}